// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:
   // deal with x: zx, nx
    Mux16(a=x, b[0..15]=false, sel=zx, out=xNew1);
    Not16(in=xNew1, out=notxNew1);
    Mux16(a=xNew1, b=notxNew1, sel=nx, out=xNew2);

    // deal with y: zy, ny
    Mux16(a=y, b[0..15]=false, sel=zy, out=yNew1);
    Not16(in=yNew1, out=notyNew1);
    Mux16(a=yNew1, b=notyNew1, sel=ny, out=yNew2);

    // get out: out = {outBufSign, outBufHigh, outBufLow}
    Add16(a=xNew2, b=yNew2, out=sumTmp);
    And16(a=xNew2, b=yNew2, out=andTmp);
    Mux16(a=andTmp, b=sumTmp, sel=f, out=outTmp);
    Not16(in=outTmp, out=notoutTmp);
    Mux16(a=outTmp, b=notoutTmp, sel=no, out[15]=outBufSign, out[0..7]=outBufLow, out[8..14]=outBufHigh);
    And16(a[15]=outBufSign, a[0..7]=outBufLow, a[8..14]=outBufHigh, b[0..15]=true, out=out);

    // get zr
    Or8Way(in=outBufLow, out=zrTmp1);
    Or8Way(in[0..6]=outBufHigh, in[7]=outBufSign, out=zrTmp2);
    Or(a=zrTmp1, b=zrTmp2, out=notzr);
    Not(in=notzr, out=zr);

    // get ng
    Mux(a=false, b=true, sel=outBufSign, out=ng);

}